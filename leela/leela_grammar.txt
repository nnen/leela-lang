Program            -> !startContext Preamble !allocLocals CompoundStmt :STOP !writeChunks !writeStrings .

Preamble           -> ConstDecl Preamble
                    | VarDecl Preamble
                    | .

ConstDecl          -> const ident equals number !addConst ConstDeclRest semicolon .
ConstDeclRest      -> comma ident equals number !addConst ConstDeclRest
                    | .
VarDecl            -> var ident !addLocal VarDeclRest semicolon .
VarDeclRest        -> comma ident !addLocal VarDeclRest
                    | .

Statement          -> CompoundStmt
                    | Assignment
                    | print RValue :PRINT
                    | if Expression then Statement ElseStmt
                    | while Expression do Statement
                    | return RValue :RETURN
                    | .
CompoundStmt       -> begin Statement StatementRest end .
StatementRest      -> semicolon Statement StatementRest
                    | .
ElseStmt           -> else Statement
                    | .

Assignment         -> ident AssignVar .
AssignVar          -> assign RValue !assignVar
                    | !getValueForLookup period ident !identToString AssignIndex
                    | !getValueForLookup left_bra RValue right_bra AssignIndex .
AssignIndex        -> assign RValue :STORE
                    | :LOAD period ident !identToString AssignIndex
                    | :LOAD left_bra RValue right_bra AssignIndex .

LValue             -> ident LValueRest .
LValueRest         -> Index LValueRest | .
RValue             -> Expression
                    | function !startFunction left_par ArgNameList right_par colon FunctionPreamble !allocLocals Statement :PUSH :RETURN !endFunction
                    | lambda !startFunction left_par ArgNameList right_par colon RValue :RETURN !endFunction .

FunctionPreamble   -> VarDecl FunctionPreamble
                    | .

Expression         -> Term ExpressionRest
                    | minus Term ExpressionRest .
ExpressionRest     -> plus Term :ADD ExpressionRest
                    | minus Term :SUB ExpressionRest
                    | .
Term               -> Factor TermRest .
TermRest           -> asterix Factor :MUL TermRest
                    | slash Factor :DIV TermRest
                    | .
Factor             -> PrimaryExpr
                    | number !pushNumber
                    | string !pushString
                    | left_par Expression right_par .
PrimaryExpr        -> ident !getSymbolValue PostfixOp .
PostfixOp          -> Index PostfixOp
                    | left_par ParamList right_par PostfixOp
                    | .
Index              -> left_bra Expression right_bra .

ParamList          -> RValue ParamListRest | .
ParamListRest      -> comma RValue ParamListRest | .

ArgNameList        -> ident !addArg ArgNameListRest
                    | .
ArgNameListRest    -> comma ident !addArg ArgNameListRest
                    | .

