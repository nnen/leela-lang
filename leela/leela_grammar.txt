Program            -> !startContext Preamble CompoundStmt :STOP .

Preamble           -> ConstDecl Preamble
                    | VarDecl Preamble
                    | .

ConstDecl          -> const ident equals number !addConst ConstDeclRest semicolon .
ConstDeclRest      -> comma ident equals number !addConst ConstDeclRest
                    | .
VarDecl            -> var ident !addLocal VarDeclRest semicolon .
VarDeclRest        -> comma ident !addLocal VarDeclRest
                    | .

Statement          -> CompoundStmt
                    | LValue assign RValue
                    | if Expression then Statement ElseStmt
                    | while Expression do Statement
                    | .
CompoundStmt       -> begin Statement StatementRest end .
StatementRest      -> semicolon Statement StatementRest
                    | .
ElseStmt           -> else Statement
                    | .

LValue             -> ident LValueRest .
LValueRest         -> Index LValueRest | .
RValue             -> Expression
                    | function left_par ArgNameList right_par colon VarDecl !startChunk !startContext Statement !endContext !endChunk
                    | lambda left_par ArgNameList right_par colon !startChunk !startContext RValue :RETURN !endContext !endChunk .

Expression         -> Term ExpressionRest
                    | minus Term ExpressionRest .
ExpressionRest     -> plus Term :ADD ExpressionRest
                    | minus Term :SUB ExpressionRest
                    | .
Term               -> Factor TermRest .
TermRest           -> asterix Factor :MUL TermRest
                    | slash Factor :DIV TermRest
                    | .
Factor             -> PrimaryExpr
                    | number !pushNumber
                    | left_par Expression right_par .
PrimaryExpr        -> ident PostfixOp .
PostfixOp          -> Index PostfixOp
                    | left_par ParamList right_par PostfixOp
                    | .
Index              -> left_bra Expression right_bra .

ParamList          -> RValue ParamListRest | .
ParamListRest      -> comma RValue ParamListRest | .

ArgNameList        -> ident !addArg ArgNameListRest
                    | .
ArgNameListRest    -> comma ident !addArg ArgNameListRest
                    | .

