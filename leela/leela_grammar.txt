Program            -> !startContext Preamble !allocLocals CompoundStmt :STOP .

Preamble           -> ConstDecl Preamble
                    | VarDecl Preamble
                    | .

ConstDecl          -> const ident equals number !addConst ConstDeclRest semicolon .
ConstDeclRest      -> comma ident equals number !addConst ConstDeclRest
                    | .
VarDecl            -> var ident !addLocal VarDeclRest semicolon .
VarDeclRest        -> comma ident !addLocal VarDeclRest
                    | .

Statement          -> CompoundStmt
                    | LValue assign RValue
                    | if Expression then Statement ElseStmt
                    | while Expression do Statement
					| return RValue :RETURN
                    | .
CompoundStmt       -> begin Statement StatementRest end .
StatementRest      -> semicolon Statement StatementRest
                    | .
ElseStmt           -> else Statement
                    | .

LValue             -> ident LValueRest .
LValueRest         -> Index LValueRest | .
RValue             -> Expression
                    | function !startFunction left_par ArgNameList right_par colon VarDecl !allocLocals Statement !endFunction
                    | lambda !startFunction left_par ArgNameList right_par colon RValue :RETURN !endFunction .

Expression         -> Term ExpressionRest
                    | minus Term ExpressionRest .
ExpressionRest     -> plus Term :ADD ExpressionRest
                    | minus Term :SUB ExpressionRest
                    | .
Term               -> Factor TermRest .
TermRest           -> asterix Factor :MUL TermRest
                    | slash Factor :DIV TermRest
                    | .
Factor             -> PrimaryExpr
                    | number !pushNumber
                    | left_par Expression right_par .
PrimaryExpr        -> ident !getSymbolValue PostfixOp .
PostfixOp          -> Index PostfixOp
                    | left_par ParamList right_par PostfixOp
                    | .
Index              -> left_bra Expression right_bra .

ParamList          -> RValue ParamListRest | .
ParamListRest      -> comma RValue ParamListRest | .

ArgNameList        -> ident !addArg ArgNameListRest
                    | .
ArgNameListRest    -> comma ident !addArg ArgNameListRest
                    | .

