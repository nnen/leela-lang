Program            -> !startContext Preamble !dumpSymbols !allocLocals CompoundStmt :STOP !writeChunks !writeStrings .

Preamble           -> ConstDecl Preamble
                    | VarDecl Preamble
                    | .

ConstDecl          -> const ident equals ConstExpr !addConst ConstDeclRest semicolon .
ConstDeclRest      -> comma ident equals ConstExpr !addConst ConstDeclRest
                    | .
ConstExpr          -> number !pushNumber 
                    | string !pushString .

VarDecl            -> var ident !addLocal VarDeclRest semicolon .
VarDeclRest        -> comma ident !addLocal VarDeclRest
                    | .

Statement          -> CompoundStmt
                    | Assignment
                    | print RValue :PRINT
                    | if Expression :NOT !ifJump then Statement ElseStmt
                    | while !startWhile Expression :NOT !whileJump do Statement !endWhile
                    | return RValue :RETURN
                    | .
CompoundStmt       -> begin Statement StatementRest end .
StatementRest      -> semicolon Statement StatementRest
                    | .
ElseStmt           -> else !elseJump Statement !ifEnd
                    | !ifEnd .

Assignment         -> ident AssignVar .
AssignVar          -> assign RValue !assignVar
                    | !pushSibling period ident !identToString AssignItem
                    | !pushSibling left_bra RValue right_bra AssignItem
                    | !pushSibling left_par ParamList !writeCall right_par :POP .
AssignItem         -> assign RValue :STORE
                    | :LOAD period ident !identToString AssignItem
                    | :LOAD left_bra RValue right_bra AssignItem
				| :LOAD left_par ParamList !writeCall right_par :POP .

RValue             -> Expression
                    | function !startFunction left_par ArgNameList right_par colon FunctionPreamble !dumpSymbols !writeLine !allocLocals Statement :PUSH :RETURN !endFunction
                    | lambda !startFunction left_par ArgNameList right_par colon !dumpSymbols !writeLine RValue :RETURN !endFunction .

FunctionPreamble   -> VarDecl FunctionPreamble
                    | .

Expression         -> Comparsion ExpressionRest .
ExpressionRest     -> and Comparsion :AND ExpressionRest
                    | or Comparsion :OR ExpressionRest
                    | .
Comparsion         -> Sum ComparsionRest .
ComparsionRest     -> equals Sum :EQUALS ComparsionRest
                    | not_equals Sum :EQUALS :NOT ComparsionRest
                    | left_angle Sum :LESS ComparsionRest
                    | right_angle Sum :GREATER ComparsionRest
                    | less_or_eq Sum :LESS_OR_EQ ComparsionRest
                    | greater_or_eq Sum :GREATER_OR_EQ ComparsionRest
                    | .
Sum                -> Term SumRest
                    | minus Term !negate SumRest .
SumRest            -> plus Term :ADD SumRest
                    | minus Term :SUB SumRest
                    | .
Term               -> Factor TermRest .
TermRest           -> asterix Factor :MUL TermRest
                    | slash Factor :DIV TermRest
                    | .
Factor             -> PrimaryExpr PostfixOp .
PrimaryExpr        -> ident !pushSymbolValue
                    | ConstExpr
                    | left_par RValue right_par
                    | left_bra :MAKE ArrayItemList right_bra .
PostfixOp          -> left_bra Expression :LOAD right_bra PostfixOp
                    | left_par ParamList !writeCall right_par PostfixOp
                    | .

ArrayItemList      -> ArrayItem ArrayItemListRest
                    | .
ArrayItemListRest  -> comma ArrayItem ArrayItemListRest
                    | .
ArrayItem          -> :DUP ident !identToString ArrayItemRest
                    | :DUP RValue ArrayItemRest
                    | .
ArrayItemRest      -> colon RValue :STORE
                    | :APPEND .

ParamList          -> RValue ParamListRest !returnPlusOne
                    | !returnZero .
ParamListRest      -> comma RValue ParamListRest !returnPlusOne
                    | !returnZero .

ArgNameList        -> ident !addArg ArgNameListRest
                    | .
ArgNameListRest    -> comma ident !addArg ArgNameListRest
                    | .

